000100*
000200*>>> START INCLUDE BDCOMMIK - KRÆVER INCLUDE AF BDCOMMIC
000300*
000400*>>> LIGELEDES SKAL VARIABLEN ANTAL-CHKP-TAKEN VÆRE DEFINERET
000500*>>> SOM PIC 9(9) VALUE 0 EFTER BD-STANDARD
000600*>>> VARIABLEN SKAL LIGGE I DET AREAL DATA SOM GEMMES VED
000700*>>> COMMIT IFLG BD.STANDARD IMELLEM FELTERNE CHKP-AREA-1
000800*>>> OG CHKP-AREA-1-END
000900*
001000*>>> DER SKAL VÆRE EN SECTION DER HEDDER 120-VI-HAR-RESTARTET
001100*>>> HVOR DER SKAL TAGES STILLING TIL HVAD DER SKAL SKE VED
001200*>>> RESTART
001300*
001400*>>> DET ER MULIGT FRA BDSPARM I JCL AT SÆTTE 2 PARAMETRE OP:
001500*>>>       CHKANT : ANTAL KALD MELLEM COMMITPUNKTER.
001600*>>>                HVIS MAN TESTER PÅ ET LAVT ANTAL
001700*>>>                TRANSER KAN DENNE MED FORDEL SÆTTES
001800*>>>                TIL 1 SÅLEDES AT MAN ER SIKKER PÅ AT
001900*>>>                DER COMMITTES!
002000*>>>                DEFAULTVÆRDI : 1000 KALD.
002100*>>>       CHKSEK : ANTAL SEKUNDER MELLEM HVERT COMMITPUNKT.
002200*>>>                HVIS DETTE SKAL BENYTTES SKAL DER I PROGRAM-
002300*>>>                MET FLYTTES 999999 TIL BDC-FREQUENCY, DETTE
002400*>>>                OVERGIVER STYRINGEN TIL CHKSEK PARAMETREN.
002500*>>>                DEFAULTVÆRDI : 10 SEK.
002600*>>> DISSE PARAMETRE KAN DYNAMISK ÆNDRES VIA DB2-TABELLEN
002700*>>> BDCSTYR.
002800*
002900********************************************************STANDARD**
003000 INC-COMMIT-INIT SECTION.
003100******************************************************************
003200
003300     INITIALIZE IDENT-AREAL IN :BDSIXXX:-PARM
003400     MOVE IDENT-AREAL IN :BDSIXXX:-PARM TO WS-COMMIT-IDENT
003500
003600     MOVE 'S'         TO BDC-REQUEST-TYPE
003700
003800     CALL 'BDCOMMIT' USING
003900           BDCOMMIT-AREA, BDCOMMIT-AREA-END,
004000           CHKP-AREA-1, CHKP-AREA-1-END
004100
004200     IF BDC-PROGRAM-IS-RESTARTING
004300     OR BDC-RETURN-OK
004400        CONTINUE
004500     ELSE
004600        MOVE 'INIT/OPEN' TO BDSMFJL-I-CALL-FUNKTION
004700        MOVE 1           TO BDSMFJL-I-PRG-POS
004800        PERFORM INC-BDCOMMIT-FEJL
004900     END-IF
005000
005100     IF BDC-PROGRAM-IS-RESTARTING AND ANTAL-CHKP-TAKEN > 0
005200        SET VI-HAR-RESTARTET TO TRUE
005300        PERFORM 120-VI-HAR-RESTARTET
005400     END-IF
005500
005600     .
005700********************************************************STANDARD**
005800 INC-COMMIT-INIT-PARALLEL SECTION.
005900******************************************************************
006000
006100     INITIALIZE IDENT-AREAL IN :BDSIXXX:-PARM
006200     MOVE IDENT-AREAL IN :BDSIXXX:-PARM TO WS-COMMIT-IDENT
006300
006400     MOVE 'S'         TO BDC-REQUEST-TYPE
006500     MOVE 'Y'         TO BDC-PARALLEL
006600
006700     CALL 'BDCOMMIT' USING
006800           BDCOMMIT-AREA, BDCOMMIT-AREA-END,
006900           CHKP-AREA-1, CHKP-AREA-1-END
007000
007100     IF BDC-PROGRAM-IS-RESTARTING
007200     OR BDC-RETURN-OK
007300        CONTINUE
007400     ELSE
007500        MOVE 'INIT/OPEN' TO BDSMFJL-I-CALL-FUNKTION
007600        MOVE 2           TO BDSMFJL-I-PRG-POS
007700        PERFORM INC-BDCOMMIT-FEJL
007800     END-IF
007900
008000     IF BDC-PROGRAM-IS-RESTARTING AND ANTAL-CHKP-TAKEN > 0
008100        SET VI-HAR-RESTARTET TO TRUE
008200        PERFORM 120-VI-HAR-RESTARTET
008300     END-IF
008400
008500     .
008600********************************************************STANDARD**
008700 INC-COMMIT SECTION.
008800******************************************************************
008900
009000     MOVE ' '    TO BDC-REQUEST-TYPE
009100
009200     IF  ANTAL-CHKP-TAKEN = ZERO
009300         MOVE 1 TO ANTAL-CHKP-TAKEN
009400     END-IF
009500
009600     CALL 'BDCOMMIT' USING
009700           BDCOMMIT-AREA, BDCOMMIT-AREA-END,
009800           CHKP-AREA-1, CHKP-AREA-1-END
009900
010000     IF BDC-RETURN-OK
010100        CONTINUE
010200     ELSE
010300        MOVE 'COMMIT' TO BDSMFJL-I-CALL-FUNKTION
010400        MOVE 3        TO BDSMFJL-I-PRG-POS
010500        PERFORM INC-BDCOMMIT-FEJL
010600     END-IF
010700
010800     IF BDC-COMMIT-WAS-TAKEN
010900        ADD 1 TO ANTAL-CHKP-TAKEN
011000     END-IF
011100
011200     .
011300********************************************************STANDARD**
011400 INC-COMMIT-FORCE SECTION.
011500******************************************************************
011600
011700     MOVE 'F'    TO BDC-REQUEST-TYPE
011800
011900     IF  ANTAL-CHKP-TAKEN = ZERO
012000         MOVE 1 TO ANTAL-CHKP-TAKEN
012100     END-IF
012200
012300     CALL 'BDCOMMIT' USING
012400           BDCOMMIT-AREA, BDCOMMIT-AREA-END,
012500           CHKP-AREA-1, CHKP-AREA-1-END
012600
012700     IF BDC-RETURN-OK
012800        CONTINUE
012900     ELSE
013000        MOVE 'COMMIT' TO BDSMFJL-I-CALL-FUNKTION
013100        MOVE 4        TO BDSMFJL-I-PRG-POS
013200        PERFORM INC-BDCOMMIT-FEJL
013300     END-IF
013400
013500     IF BDC-COMMIT-WAS-TAKEN
013600        ADD 1 TO ANTAL-CHKP-TAKEN
013700     END-IF
013800
013900     .
014000********************************************************STANDARD**
014100 INC-COMMIT-WOULD SECTION.
014200******************************************************************
014300
014400     MOVE 'W'    TO BDC-REQUEST-TYPE
014500
014600     CALL 'BDCOMMIT' USING
014700           BDCOMMIT-AREA, BDCOMMIT-AREA-END,
014800           CHKP-AREA-1, CHKP-AREA-1-END
014900
015000     IF BDC-RETURN-OK
015100        CONTINUE
015200     ELSE
015300        MOVE 'COMMIT' TO BDSMFJL-I-CALL-FUNKTION
015400        MOVE 5        TO BDSMFJL-I-PRG-POS
015500        PERFORM INC-BDCOMMIT-FEJL
015600     END-IF
015700
015800     IF BDC-COMMIT-WAS-TAKEN
015900        ADD 1 TO ANTAL-CHKP-TAKEN
016000     END-IF
016100
016200     .
016300********************************************************STANDARD**
016400 INC-COMMIT-SLUT SECTION.
016500******************************************************************
016600
016700     MOVE 'E'    TO BDC-REQUEST-TYPE
016800
016900     CALL 'BDCOMMIT' USING
017000           BDCOMMIT-AREA, BDCOMMIT-AREA-END,
017100           CHKP-AREA-1, CHKP-AREA-1-END
017200
017300     IF BDC-RETURN-OK
017400        CONTINUE
017500     ELSE
017600        MOVE 'AFSLUT/CLOSE' TO BDSMFJL-I-CALL-FUNKTION
017700        MOVE 6              TO BDSMFJL-I-PRG-POS
017800        PERFORM INC-BDCOMMIT-FEJL
017900     END-IF
018000
018100     ADD 1 TO ANTAL-CHKP-TAKEN
018200     .
018300******************************************************************
018400 INC-ROLL-BACK SECTION.
018500******************************************************************
018600
018700     MOVE 'E'    TO BDC-REQUEST-TYPE
018800     MOVE 'R'    TO BDC-ROLLBACK
018900
019000     CALL 'BDCOMMIT' USING
019100           BDCOMMIT-AREA, BDCOMMIT-AREA-END,
019200           CHKP-AREA-1, CHKP-AREA-1-END
019300
019400     IF BDC-RETURN-OK
019500        CONTINUE
019600     ELSE
019700        MOVE 'ROLLBACK' TO BDSMFJL-I-CALL-FUNKTION
019800        MOVE 7          TO BDSMFJL-I-PRG-POS
019900        PERFORM INC-BDCOMMIT-FEJL
020000     END-IF
020100
020200     IF BDC-COMMIT-WAS-TAKEN
020300        ADD 1 TO ANTAL-CHKP-TAKEN
020400     END-IF
020500
020600     .
020700******************************************************************
020800 INC-BDCOMMIT-FEJL SECTION.
020900******************************************************************
021000
021100     MOVE 999                        TO BDSMFJL-I-STATU-KD
021200
021300     MOVE WS-COMMIT-IDENT            TO BDSMFJL-IDENT
021400
021500     MOVE 'FEJL VED KALD BDCOMMIT '  TO BDSMFJL-I-BESKRIV
021600
021700     INITIALIZE BDSMFJL-I-DATA-CALL
021800     SET  BDSMFJL-FUNK-CALL          TO TRUE
021900     MOVE 'BDCOMMIT'                 TO BDSMFJL-I-CALL-PRG
022000     MOVE 'BDCOMMIT-AREA'            TO BDSMFJL-I-CALL-MEDD1
022100     MOVE BDCOMMIT-AREA              TO BDSMFJL-I-CALL-VAR1
022200     MOVE CHKP-AREA-1 TO BDSMFJL-I-CALL-VAR2
022300       (1: FUNCTION MIN(LENGTH OF CHKP-AREA-1
022400                        LENGTH OF BDSMFJL-I-CALL-VAR2))
022500     MOVE 0099 TO WS-RETURN-CODE
022600     PERFORM 999-FEJLMELD
022700
022800     .
022900*
023000*<<< SLUT INCLUDE BDCOMMIK
023100*
